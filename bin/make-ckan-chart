#!/usr/bin/env perl
use strict;
use warnings;
use lib 'lib';
use Net::CKAN;
use URI::GoogleChart;

my $MAX_MINISTRIES = 5;

my $ckan = Net::CKAN->new(
    server => 'http://ca.ckan.net',
    api_key => '53c7b7cb-b771-45f0-86f3-e7613e760ddb',
);
my $packages = $ckan->get_packages;

my %pkg_count;
for my $pkg_name (@$packages) {
    warn "Fetching $pkg_name\n";
    my $pkg = $ckan->get_package("$pkg_name");
    if (my $ministry = $pkg->{extras}{Ministry}) {
        $pkg_count{$ministry}++;
    }
}

my @data;
my @x_labels;
my @ministries = sort { $pkg_count{$b} <=> $pkg_count{$a} } keys %pkg_count;
my $max = 1;
for my $ministry (splice @ministries, 0, $MAX_MINISTRIES) {
    my $count = $pkg_count{$ministry};
    $max = $count if $max < $count;
    push @data, [$count];
    push @x_labels, $ministry;
}
print URI::GoogleChart->new("vertical-grouped-bars", 600, 300,
    data => \@data,
    label => \@x_labels,
    chxt => 'y',
    chxr => "0,0,$max,1",
    color => [qw/red green blue yellow purple/],
), "\n";
exit;

